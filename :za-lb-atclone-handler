#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
#
# Original work Copyright (c) 2019-2020 Sebastian Gniazdowski
# Modified work Copyright (c) 2020-2021 Nicholas Serrano
# Modified work Copyright (c) 2022 zdharma-continuum and contributors

emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops nullglob

if [[ "$1" = plugin ]]; then
  local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
else
  local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
fi

#
# lbin'' ice â€“ creation of links
#

if (( ${+ICE[sbin]} )); then
  (( !OPTS[opt_-q,--quiet] )) && \
    +zinit-message "{pre}linkbin annex: {ice}sbin{warn} ice detected, not doing anything...{rst}"
  return;
fi

if (( ${+ICE[lbin]} )) {

  local lbin sym="-P"
  local -a lbins srcdst
  lbins=( ${(s.;.)ICE[lbin]} )

  (
    # CD for the globbing through eval
    builtin cd -q "$dir" || return

    for lbin ( $lbins "" ) {
      [[ ${lbin[1]} = "!" ]] && sym="-s"

      if [[ -z $lbin && ${#lbins} -eq 0 ]] || [[ $lbin = "!" && ${#lbins} -eq 1 ]]; then
        local -a files
        integer i=0
        while [[ ! -f $files[1] && $i -lt 9 ]]; do
          ((i++))
          case $i in
            1) files=( $dir/${id_as:t}(Nnon.) )
              ;;
            2) [[ -n $plugin ]] && files=( $dir/$plugin(Nnon.) )
              ;;
            3) [[ -n $url ]] && files=( $dir/${url:t}(Nnon.) )
              ;;
            4) files=( $dir/*(*Nnon.:t) )
              ;;
            5) files=( $dir/**/${id_as:t} )
              ;;
            6) [[ -n $plugin ]] && files=( $dir/**/$plugin(Nnon.) )
              ;;
            7) [[ -n $url ]] && files=( $dir/**/${url:t}(Nnon.) )
              ;;
            8) files=( $dir/**/*(*Nnon.:t) )
              ;;
            9) +zinit-message "{pre}linkbin annex: {error}The automatic-empty {ice}lbin{error} ice didn't find any executable files{rst}"
              ;;
          esac
        done
        [[ $i -gt 8 ]] && break
        lbin=$files[1]
      else
        lbin=${lbin#!}
        [[ -z $lbin ]] && continue
      fi

      # +zinit-message "{pre}linkbin: {info} srcdst value #1: {glob}$srcdst[@]{info}{rst}"
      srcdst=( ${(@s.->.)lbin} )
      # +zinit-message "{pre}linkbin: {info} srcdst value #2: {glob}$srcdst[@]{info}{rst}"
      srcdst=( "${srcdst[@]//((#s)[[:space:]]##|[[:space:]]##(#e))/}" )
      # srcdst=( ${${srcdst[@]## ##}%% ##} )
      # srcdst=(${(MS)srcdst[@]##[[:graph:]]*[[:graph:]]})
      # +zinit-message "{pre}linkbin: {info} srcdst value #3: {glob}$srcdst[@]{info}{rst}"

      @zinit-substitute "srcdst[1]" "srcdst[2]"

      local -a fnames
      local fname
      eval "fnames=( ${srcdst[1]}(Nnon.) )"

      if (( !${#fnames} )); then
        +zinit-message "{pre}linkbin annex: {error}The {ice}lbin{error} ice (\`{glob}$lbin{error}')didn't match any files{rst}"
        continue
      fi

      for fname ( $fnames ) {
        srcdst[1]="$fname"

        local target_binary="${${(M)srcdst[1]:#/*}:-$dir/${srcdst[1]}}"
        local fnam="${srcdst[2]:-${srcdst[1]:t}}"
        local file="$ZPFX/bin/$fnam"

        command chmod +x "$target_binary"
        command ln -f "$sym" "$target_binary" "$file"

        if [[ -x $file ]]; then
          if (( !OPTS[opt_-q,--quiet] )) && [[ -x $target_binary ]]; then
            if [[ $hook == atclone-<-> ]] || [[ $ZINIT[annex-multi-flag:pull-active] -ge  2 ]]; then
              +zinit-message "{pre}linkbin annex: {msg}${${${hook:#*atclone-<->}:+Re-c}:-C}reated the {obj}$fnam{data2} ${${${sym#-P}:+soft}:-hard}{msg} link and set +x on the {obj}${target_binary:t}{msg} binary{rst}"
            else
              +zinit-message "{pre}linkbin annex: {error}${${${hook:#*atclone-<->}:+Re-c}:-C}reated the {obj}$fnam{data2} ${${${sym#-P}:+soft}:-hard}{error} link however the {obj}${target_binary:t}{error} binary does not exist or failed to set +x on it{rst}"
            fi
          fi
        else
          +zinit-message "{pre}linkbin annex: {error}Something went wrong creating the {obj}$fnam{error} link{rst}"
        fi
      }
    }
  )
}
