#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-
#
# Original work Copyright (c) 2019-2020 Sebastian Gniazdowski
# Modified work Copyright (c) 2020-2021 Nicholas Serrano
# Modified work Copyright (c) 2022 zdharma-continuum and contributors
#

emulate -RL zsh
setopt extendedglob noshortloops nullglob typesetsilent warncreateglobal

if [[ "$1" = plugin ]]; then
    local dir="${5#%}" hook="$6" id_as="$4" plugin="$3" type="$1" user="$2"
else
    local dir="${4#%}" hook="$5" id_as="$3" type="$1" url="$2"
fi
if (( ${+ICE[lbin]} )); then
    (( ${+ICE[sbin]} )) && {
        (( !OPTS[opt_-q,--quiet] )) && +zinit-message "{w} linkbin annex disabled due to {ice}sbin{rst} ice detected"
        return
    }
    local -a lbins srcdst
    lbins=(${(s.;.)ICE2[lbin]})
    local lbin sym="-P"
    (
        builtin cd -q "$dir" || return
        for lbin in $lbins ""; do
            [[ ${lbin[1]} = "!" ]] && sym="-s"
            if [[ -z $lbin && ${#lbins} -eq 0 ]] || [[ $lbin = "!" && ${#lbins} -eq 1 ]]; then
                local -a files
                integer i=0
                while [[ ! -f $files[1] && $i -lt 9 ]]; do
                    ((i++))
                    case $i in
                        (1)
                            files=($dir/${id_as:t}(Nnon.))
                            ;;
                        (2)
                            [[ -n $plugin ]] && files=($dir/$plugin(Nnon.))
                            ;;
                        (3)
                            [[ -n $url ]] && files=($dir/${url:t}(Nnon.))
                            ;;
                        (4)
                            files=($dir/*(*Nnon.:t))
                            ;;
                        (5)
                            files=($dir/**/${id_as:t})
                            ;;
                        (6)
                            [[ -n $plugin ]] && files=($dir/**/$plugin(Nnon.))
                            ;;
                        (7)
                            [[ -n $url ]] && files=($dir/**/${url:t}(Nnon.))
                            ;;
                        (8)
                            files=($dir/**/*(*Nnon.:t))
                            ;;
                        (9)
                            +zinit-message "{w} linkbin annex ice did not find any executable files"
                            ;;
                    esac
                done
                [[ $i -gt 8 ]] && break
                lbin=$files[1]
            else
                lbin=${lbin#!}
                [[ -z $lbin ]] && continue
            fi
            srcdst=(${(@s.->.)lbin})
            srcdst=("${srcdst[@]//((#s)[[:space:]]##|[[:space:]]##(#e))/}")
            # `
            @zinit-substitute 'srcdst[1]' 'srcdst[2]'
            local -a fnames
            local fname
            # eval "fnames=(${srcdst[1]}(Nnon.))"
            builtin print -v fnames -Pr -- ${srcdst[1]}(Nnon.)
            print -l -- "filenames ${#fnames}" $fnames
            if (( !${#fnames} )); then
                +zinit-message "{e} linkbin annex ice {glob}$lbin{rst} did not match any files"
                continue
            fi
            for fname in $fnames; do
                srcdst[1]="$fname"
                local fnam="${srcdst[2]:-${srcdst[1]:t}}"
                local file="$ZPFX/bin/$fnam"
                if [[ -f $file ]]; then
                    command rm -- "$file"
                    if ! [[ -f $file ]]; then
                        (( !OPTS[opt_-q,--quiet] )) && +zinit-message "{m} linkbin annex removed {file}$fnam{rst} ${${${sym#-P}:+soft}:-hard} link"
                    else
                        +zinit-message "{e} linkbin annex encountered unhandled error deleting {file}$fnam{rst} link"
                    fi
                else
                    +zinit-message "{w} linkbin annex did not find {file}$fnam{rst} ${${${sym#-P}:+soft}:-hard} link in \$ZPFX/bin"
                fi
            done
        done
    )
fi
